/*
 * Copyright 2017 Banco Bilbao Vizcaya Argentaria, S.A.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            url 'http://repo.jenkins-ci.org/releases/'
        }
    }

    dependencies {
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.18.1'
    }
}

apply plugin: 'net.linguica.maven-settings'
apply plugin: 'org.jenkins-ci.jpi'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8

def isReleaseVersion = !version.endsWith("SNAPSHOT")
def user = hasProperty('mavenUser') ? mavenUser : System.getenv('mavenUser')
def password = hasProperty('mavenPassword') ? mavenPassword : System.getenv('mavenPassword')

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

repositories {
    mavenCentral()
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/reports/coverage"
    }
}

jenkinsPlugin {
    // always use latest LTS!
    coreVersion = '2.7.1'
    shortName = pluginName
    displayName = jenkinsDisplayName
}

configurations.archives.artifacts.clear();
artifacts {
    archives file: project.file("build/libs/mirrorgate-publisher.hpi")
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: user, password: password)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: user, password: password)
            }

            pom.project {
                name pluginName
                artifactId pluginName
                packaging 'hpi'
                description 'WallBoard application meant to give teams fast feedback in all the different areas related to software development.'

                url 'https://github.com/bbva/mirrorgate-jenkins-builds-collector'

                scm {
                    url 'https://github.com/bbva/mirrorgate-jenkins-builds-collector'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'mirrorgate'
                        name 'mirrorgate.group@bbva.com'
                    }
                }
            }
        }
    }
}

defaultTasks 'clean', 'jpi'

dependencies {
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.0.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    
    testCompile group: 'org.powermock', name: 'powermock-mockito-release-full', version: '1.6.4'
}
