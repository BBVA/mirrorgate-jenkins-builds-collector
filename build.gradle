/*
 * Copyright 2017 Banco Bilbao Vizcaya Argentaria, S.A.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        maven {
            url 'https://repo.jenkins-ci.org/releases/'
        }
    }

    dependencies {
        classpath 'net.linguica.gradle:maven-settings-plugin:0.5'
        classpath 'org.jenkins-ci.tools:gradle-jpi-plugin:0.39.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.28.0'
    }
}

apply plugin: 'jpi'
apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: "com.github.ben-manes.versions"

def isReleaseVersion = !version.endsWith("SNAPSHOT")
def user = hasProperty('mavenUser') ?: System.getenv('mavenUser')
def pass = hasProperty('mavenPassword') ?: System.getenv('mavenPassword')

repositories {
    mavenCentral()
    jcenter()
    maven {
        url 'https://plugins.gradle.org/m2/'
    }
    maven {
        url 'https://repo.jenkins-ci.org/releases/'
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}

jenkinsPlugin {
    shortName = pluginName
    displayName = jenkinsDisplayName
    coreVersion = '2.240'

    configureRepositories = false // Needed to publish in Sonatype
    configurePublishing = false // Needed to publish in Sonatype
}

configurations.archives.artifacts.clear()
def hpiArtifact = artifacts.add('archives', file('build/libs/mirrorgate-publisher.hpi'))

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId pluginName
            artifact hpiArtifact

            pom {
                name = pluginName
                description = 'WallBoard application meant to give teams fast feedback in all the different areas related to software development.'
                url = 'https://github.com/bbva/mirrorgate-jenkins-builds-collector'

                packaging 'hpi'


                scm {
                    url = 'https://github.com/bbva/mirrorgate-jenkins-builds-collector'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mirrorgate'
                        name = 'mirrorgate.group@bbva.com'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            if (isReleaseVersion) {
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            } else {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            }
            credentials {
                username user
                password pass
            }
        }
    }
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign publishing.publications.mavenJava
}

task replaceVersionInREADME {
    ant.replaceregexp(match:'Last version of this plugin \\(v([0-9\\.]+(-SNAPSHOT)?)\\)', replace:"Last version of this plugin (v$version)", flags:'g', byline:true) {
        fileset(dir: '.', includes: 'README.md')
    }

    ant.replaceregexp(match:'Jenkins version ([0-9\\.]+)', replace:"Jenkins version ${jenkinsPlugin.coreVersion}", flags:'g', byline:true) {
        fileset(dir: '.', includes: 'README.md')
    }
}

defaultTasks 'clean', 'jpi'

dependencies {
    implementation(
        'com.fasterxml.jackson.core:jackson-databind:2.11.0',
        'org.apache.commons:commons-lang3:3.10'
    )

    /* Jenkins Plugins */
    implementation(
        'org.jenkins-ci.plugins:apache-httpcomponents-client-4-api:4.5.10-2.0',
        'org.jenkins-ci.plugins:credentials:2.3.8',
        'org.jenkins-ci.plugins:plain-credentials:1.7'
    )

    testImplementation(
        'org.powermock:powermock-module-junit4:2.0.7',
        'org.powermock:powermock-api-mockito2:2.0.7'
    )
}